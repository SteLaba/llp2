section .text
global find_word
extern string_equals

;Параметры:
;rdi - указатель на нуль-терминированную строку
;rsi - указатель на последнее слово в словаре, т.е. на первый элемент нашего словаря
;Возвращаемое значение:
;rax - адрес слова если оно найдено, иначе 0

find_word:
.loop:
	push rdi		;так как string_equals ломает регистры rdi, rsi
	push rsi		;сохраняем их значения в стеке
	add rsi, 8		;пропускаем первых 8 байт, так как в них хранится адрес сл. элемента
	call string_equals	;в rax либо 1(стрингы равны) либо 0(стрингы неравны)
	pop rsi			;воостанавливаем значение в регистре rsi
	pop rdi			;воостанавливаем значение в регистре rdi
	test rax, rax		;проверяем значение в rax
	jnz .found		;если rax не равен нулю, нашли слово в словаре
	mov rsi, [rsi]		;в rsi загружаем адрес следующего элемента
	test rsi, rsi		;проверяем значение в rsi(если нуль то это последний элемент)
	jnz .loop		;если не нуль, зацикливаемся так как есть еще элементов
	xor rax, rax		;очищаем аккумулятор
	ret
.found:
	mov rax, rsi		;в rax загружаем содержимое rsi(адрес элемента списка найденого слова)
	ret
